buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3"
  }
}

apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven'
apply plugin: 'signing'

group = "org.mikeneck"
version = "0.1"

ext {
  canRelease = project.hasProperty('sonatypeUrl') && project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly "org.jetbrains:annotations:15.0"
  compileOnly "org.projectlombok:lombok:1.16.14"

  testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0-M3"
  testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.0-M3"
}

jar {
  baseName = project.name
}

task javadocJar(type: Jar) {
  baseName = project.name
  classifier = 'javadoc'
  from tasks.javadoc
}

task sourcesJar(type: Jar) {
  baseName = project.name
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives tasks.javadocJar, tasks.sourcesJar
}

signing {
  sign configurations.archives
}


if (canRelease) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment ->
          signing.signPom(deployment)
        }

        repository (url: sonatypeUrl) {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          name "data-utilities"
          packaging "jar"
          description "Basic data utilities for Java 8"
          url "https://github.com/mike-neck/data-utilities"

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id "mikeneck"
              name "Shinya Mochida"
              email "jkrt3333[at]gmail.com"
            }
          }
        }
      }
    }
  }
}